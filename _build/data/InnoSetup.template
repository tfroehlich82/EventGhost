[Setup]
AppCopyright=(C) 2005-2016 EventGhost Project
AppId=EventGhost
AppMutex=Global\EventGhost:7EB106DC-468D-4345-9CFE-B0021039114B
AppName=EventGhost
AppPublisher=EventGhost Project
AppPublisherURL=http://www.eventghost.net/
AppSupportURL=http://www.eventghost.net/
AppUpdatesURL=http://www.eventghost.net/
AppVerName=EventGhost %(appVersion)s
AppVersion=%(appVersion)s
ChangesAssociations=yes
Compression=lzma/ultra
DefaultDirName={pf}\EventGhost
DefaultGroupName=EventGhost
DisableReadyPage=yes
InfoBeforeFile=%(dataDir)s\LICENSE.RTF
InternalCompressLevel=ultra
OutputBaseFilename=%(appName)s_%(appVersion)s_Setup
OutputDir=%(outputDir)s
ShowLanguageDialog=auto
SolidCompression=yes
VersionInfoDescription=EventGhost Installer
#if Pos('WIP-', '%(appVersion)s')
VersionInfoVersion=0.0.0
#elif Pos('-', '%(appVersion)s')
VersionInfoVersion={#Copy('%(appVersion)s', 1, Pos('-', '%(appVersion)s') - 1)}
#else
VersionInfoVersion=%(appVersion)s
#endif

[Code]
function InitializeSetup: Boolean;
var
  MS, LS: Cardinal;
begin
  if GetVersionNumbers(ExpandConstant('{sys}\gdiplus.dll'), MS, LS) then
    Result := true
  else
    begin
      Result := false;
      MsgBox('You need to install GDI+ first.'#13#10#13#10 + 'Please visit http://www.eventghost.net/docs/faq.html for instructions.', MBError, MB_OK);
    end
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
  ResultCode: Integer;
  Subkey: String;
  Uninstall: String;
begin
  Subkey := 'Software\Microsoft\Windows\CurrentVersion\Uninstall\EventGhost_is1';
  if (CurStep = ssInstall) then begin
    if RegQueryStringValue(HKCU, Subkey, 'UninstallString', Uninstall) or RegQueryStringValue(HKLM, Subkey, 'UninstallString', Uninstall) then begin
      // Rename folders that previous versions might remove on uninstall.
      RenameFile(ExpandConstant('{commonappdata}\EventGhost'), ExpandConstant('{commonappdata}\EventGhost.bak'))
      RenameFile(ExpandConstant('{userappdata}\EventGhost'), ExpandConstant('{userappdata}\EventGhost.bak'))

      // Silently run uninstall command.
      Exec(RemoveQuotes(Uninstall), '/SILENT', '', SW_SHOWNORMAL, ewWaitUntilTerminated, ResultCode);

      // Rename folders back.
      RenameFile(ExpandConstant('{commonappdata}\EventGhost.bak'), ExpandConstant('{commonappdata}\EventGhost'))
      RenameFile(ExpandConstant('{userappdata}\EventGhost.bak'), ExpandConstant('{userappdata}\EventGhost'))
    end;
  end;
end;

[Icons]
Name: "{group}\EventGhost"; Filename: "{app}\EventGhost.exe"
Name: "{group}\EventGhost Help"; Filename: "{app}\EventGhost.chm"
Name: "{group}\EventGhost Web Site"; Filename: "http://www.eventghost.net/"
Name: "{group}\Uninstall EventGhost"; Filename: "{uninstallexe}"
Name: "{commondesktop}\EventGhost"; Filename: "{app}\EventGhost.exe"; Tasks: desktopicon
Name: "{userstartup}\EventGhost"; Filename: "{app}\EventGhost.exe"; Parameters: "-h -e OnInitAfterBoot"; Tasks: startupicon

[Languages]
Name: English; MessagesFile: "compiler:Default.isl"
Name: Armenian; MessagesFile: "compiler:Languages\Armenian.islu"
Name: BrazilianPortuguese; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
Name: Catalan; MessagesFile: "compiler:Languages\Catalan.isl"
Name: Corsican; MessagesFile: "compiler:Languages\Corsican.isl"
Name: Czech; MessagesFile: "compiler:Languages\Czech.isl"
Name: Danish; MessagesFile: "compiler:Languages\Danish.isl"
Name: Dutch; MessagesFile: "compiler:Languages\Dutch.isl"
Name: Finnish; MessagesFile: "compiler:Languages\Finnish.isl"
Name: French; MessagesFile: "compiler:Languages\French.isl"
Name: German; MessagesFile: "compiler:Languages\German.isl"
Name: Greek; MessagesFile: "compiler:Languages\Greek.isl"
Name: Hebrew; MessagesFile: "compiler:Languages\Hebrew.isl"
Name: Hungarian; MessagesFile: "compiler:Languages\Hungarian.isl"
Name: Italian; MessagesFile: "compiler:Languages\Italian.isl"
Name: Japanese; MessagesFile: "compiler:Languages\Japanese.isl"
Name: Nepali; MessagesFile: "compiler:Languages\Nepali.islu"
Name: Norwegian; MessagesFile: "compiler:Languages\Norwegian.isl"
Name: Polish; MessagesFile: "compiler:Languages\Polish.isl"
Name: Portuguese; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: Russian; MessagesFile: "compiler:Languages\Russian.isl"
Name: ScottishGaelic; MessagesFile: "compiler:Languages\ScottishGaelic.isl"
Name: SerbianCyrillic; MessagesFile: "compiler:Languages\SerbianCyrillic.isl"
Name: SerbianLatin; MessagesFile: "compiler:Languages\SerbianLatin.isl"
Name: Slovenian; MessagesFile: "compiler:Languages\Slovenian.isl"
Name: Spanish; MessagesFile: "compiler:Languages\Spanish.isl"
Name: Turkish; MessagesFile: "compiler:Languages\Turkish.isl"
Name: Ukrainian; MessagesFile: "compiler:Languages\Ukrainian.isl"

[Registry]
Root: HKCR; Subkey: ".egtree"; ValueType: string; ValueName: ""; ValueData: "EventGhost Tree"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "EventGhost Tree"; ValueType: string; ValueName: ""; ValueData: "EventGhost Tree"; Flags: uninsdeletekey
Root: HKCR; Subkey: "EventGhost Tree\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\EventGhost.exe,0"
Root: HKCR; Subkey: "EventGhost Tree\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\EventGhost.exe"" ""%%1"""

Root: HKCR; Subkey: ".egplugin"; ValueType: string; ValueName: ""; ValueData: "EventGhost Plugin"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "EventGhost Plugin"; ValueType: string; ValueName: ""; ValueData: "EventGhost Plugin"; Flags: uninsdeletekey
Root: HKCR; Subkey: "EventGhost Plugin\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\EventGhost.exe,0"
Root: HKCR; Subkey: "EventGhost Plugin\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\EventGhost.exe"" ""%%1"""

[Run]
Filename: "{app}\EventGhost.exe"; Parameters: "-install"
Filename: "{app}\EventGhost.exe"; Flags: postinstall nowait skipifsilent

[Tasks]
Name: "desktopicon"; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}
Name: "startupicon"; Description: {cm:AutoStartProgram,{#SetupSetting('AppName')}}; GroupDescription: {cm:AutoStartProgramGroupDescription}

[UninstallDelete]
Type: dirifempty; Name: "{app}"
Type: files; Name: "{userstartup}\EventGhost.lnk"

[UninstallRun]
Filename: "{app}\EventGhost.exe"; Parameters: "-uninstall"
